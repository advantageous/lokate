
/*
 *
 *  Copyright (c) 2016. Rick Hightower, Geoff Chandler
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *    		http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

plugins {
    id 'java'
    id 'signing'
    id "maven"
    id "net.saliman.cobertura" version "2.3.2"
    id "io.advantageous.docker-test" version "0.1.6"
}

group 'io.advantageous.discovery'
version '1.2.0'

sourceCompatibility = '1.8'



task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

test.onlyIf { !Boolean.getBoolean('skip.tests') }

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'io.vertx:vertx-core:3.2.1'
    compile 'io.advantageous.reakt:reakt:2.8.17'
    compile 'io.advantageous.reakt:reakt-vertx:2.1.0.RELEASE'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'ch.qos.logback:logback-core:1.1.3'
    testCompile 'ch.qos.logback:logback-classic:1.1.3'
    testCompile 'io.advantageous.gradle:docker-test-plugin:0.1.3'
}

testDockerContainers {
    consul {
        portMapping container: "8400", host: '8400'
        portMapping container: "8500", host: '8500'
        portMapping container: '53/udp', host: "8600"
        containerName 'consul'
        image 'progrium/consul'
        runArgs '-server -bootstrap -dc docker -ui-dir /ui'
        waitAfterRun 3
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/releases/' }
}

//TODO: this functionality should be in the docker-test plugin
test {
    useJUnit {
        excludeCategories 'io.advantageous.test.DockerTest'
    }
}

cobertura {
    coverageFormats = ["html", "xml"]
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required false
    sign configurations.archives
}



uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                try {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                } catch (MissingPropertyException ignore) {
                }
            }

            pom.project {
                packaging 'jar'
                name project.name
                description "Reactive Discovery client for Consul and DNS"

                url 'https://github.com/advantageous/service-discovery-lib'

                scm {
                    url 'scm:git@github.com:advantageous/service-discovery-lib.git'
                    connection 'scm:git@github.com:advantageous/service-discovery-lib.git'
                    developerConnection 'scm:git@github.com:advantageous/service-discovery-lib.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'richardHightower'
                        name 'Richard Hightower'
                    }
                    developer {
                        id 'sailorgeoffrey'
                        name 'Geoffrey Chandler'
                    }
                }
            }
        }
    }
}